plugins {
    id "com.jfrog.artifactory" version "4.31.0"
}

description 'JNI Java library (cbc-java) for COIN-OR CBC Solver'

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: "com.jfrog.artifactory"


    sourceCompatibility = 11
    targetCompatibility = 11
    group 'de.unijena.bioinf.cbc-java'
    version '2.10.8.5' //corresponds tp cbc version

    publishing {
        publications {
            javaCBC(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                url = project.findProperty("de.unijena.bioinf.build.artifactory.url")
                name = "GitLab"
                credentials(HttpHeaderCredentials) {
                    name = project.findProperty("de.unijena.bioinf.build.artifactory.token.name")
                    value = project.findProperty("de.unijena.bioinf.build.artifactory.token")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }
    println System.getProperty("os.arch").toLowerCase();
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url "https://bio.informatik.uni-jena.de/repository/libs-oss"
        metadataSources {
            mavenPom()
            artifact()
        }
    }
}

dependencies {
    //all sub projects as dependency
    implementation 'cz.adamh:native-utils:1.1-SNAPSHOT'
    implementation 'org.slf4j:slf4j-api:2.0.6'
    testImplementation project('cbc-java-jni-linux-x86-64')
    testImplementation project('cbc-java-native-linux-x86-64')
//testImplementation project( 'cbc-java-jni-linux-arm64')
//testImplementation project( 'cbc-java-native-linux-arm64')
    testImplementation project('cbc-java-jni-win-x86-64')
    testImplementation project('cbc-java-native-win-x86-64')
    testImplementation project('cbc-java-jni-mac-x86-64')
    testImplementation project('cbc-java-native-mac-x86-64')
    testImplementation project('cbc-java-jni-mac-arm64')
    testImplementation project('cbc-java-native-mac-arm64')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation("org.junit.vintage:junit-vintage-engine:5.10.0")
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

test {
    useJUnitPlatform()
}

//artifactoryPublish.skip = true
artifactory {
    contextUrl = 'https://bio.informatik.uni-jena.de/repository/'
    publish {
        repository {
            if (version.toString().endsWith('-SNAPSHOT'))
                repoKey = 'libs-snapshot-local'
            else
                repoKey = 'libs-release-local'

            username = project.findProperty('de.unijena.bioinf.build.artifactory.username')
            password = project.findProperty('de.unijena.bioinf.build.artifactory.password')
        }
        defaults {
            publications('javaCBC')
        }
    }
}

